Describe execution
	Before all
		call PrepareBakaupDirectory()
	End

	After all
		call DestructBakaupDirectory()
	End

	Context BakaupBackupExecute
		It doing automatic backup file when write file
			BakaupBackupExecute
			let bakaup_auto_madedir = glob(g:bakaup_backup_dir . '/*')
			let backup_is_exists    = strchars(bakaup_auto_madedir) isnot 0
			Assert Equals(backup_is_exists, 1)
		End
	End

	Context BakaupArchiveBackups
		" regex pattern for shell grep
		let l:DAILY_PATTERN = '[0-9][0-9][0-9][0-9]-[0-9][0-9]-[0-9][0-9]' | lockvar l:DAILY_PATTERN

		Context should be right functioned
			It create archive
				BakaupArchiveBackups
				let l:archive_name  = 'vim-bakaup_' . strftime('%Y-%m-%d', localtime()) . '.tar.bz2'
				let l:files         = GetFiles(g:bakaup#archive_dir)
				let l:names         = map(l:files, 'fnamemodify(v:val, ":t")')
				let l:archive_exist = index(l:names, l:archive_name)
				" Archive was created
				Assert (l:archive_exist isnot -1)
			End

			It clean up files
				try
					" Create file in directory
					BakaupBackupExecute
				catch
					execute ':Fail caught exception: ' v:exception
				endtry

				let l:dirs_before  = GetFiles(g:bakaup_backup_dir)
				let l:bdirs_before = filter(l:dirs_before, 'v:val =~# "'.l:DAILY_PATTERN.'$"')
				" should not be remain dirs
				Assert Equals(empty(l:bdirs_before), 0)

				try
					" Create archive
					" and Clean up directory that backed up
					BakaupArchiveBackups
				catch
					execute ':Fail caught exception: ' v:exception
				endtry

				let l:dirs_after  = GetFiles(g:bakaup_backup_dir)
				let l:bdirs_after = filter(l:dirs_after, 'v:val =~# "'.l:DAILY_PATTERN.'$"')
				" should be cleaned dirs
				Assert Equals(empty(l:bdirs_after), 1)
			End
		End

		It should not made bad side effects
			let l:UNRELATED_FILE_PATH = g:bakaup_backup_dir . '/unrelated.dummy' | lockvar l:UNRELATED_FILE_PATH

			try
				" Create unrelated file
				execute ':write' l:UNRELATED_FILE_PATH
				" Get exists unrelated file
				let l:dirs_before      = GetFiles(g:bakaup_backup_dir)
				let l:unrelated_before = filter(l:dirs_before, 'v:val !~# "'.l:DAILY_PATTERN.'$"')
				try
					BakaupArchiveBackups
				catch
					execute ':Fail caught exception: ' v:exception
				endtry
				let l:dirs_after      = GetFiles(g:bakaup_backup_dir)
				let l:unrelated_after = filter(l:dirs_before, 'v:val !~# "'.l:DAILY_PATTERN.'$"')
				Assert Equals(l:unrelated_before, l:unrelated_after)
			finally
				" After care
				call RemoveFile(l:UNRELATED_FILE_PATH)
			endtry
		End
	End

	Context BakaupSetBakaupDir
		It should be right set g:bakaup_backup_dir and g:bakaup#archive_dir
			let l:target_dir = expand('~/ahobaka_dir')
			execute ':BakaupSetBackupDir' l:target_dir
			Assert Equals(g:bakaup_backup_dir, l:target_dir)
			Assert Equals(g:bakaup#archive_dir, l:target_dir . '/archive')
		End
	End
End
